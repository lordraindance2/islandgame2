const OLD_MAN_OW_ID = 12

mapscripts ISLANDGAME_FRESA_FARMS_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: ISLANDGAME_FRESA_FARMS_HandleDaycareState
}

//daycare stuff:
script ISLANDGAME_FRESA_FARMS_HandleDaycareState {
    specialvar(VAR_RESULT, GetDaycareState)
    switch (var(VAR_RESULT)) {
       case DAYCARE_NO_MONS:
           setflag(FLAG_TEMP_A)
           setflag(FLAG_TEMP_B)
           return
       case DAYCARE_TWO_MONS:
           //setvar(VAR_OBJ_GFX_ID_B, VAR_0x800B)
           //setvar(VAR_OBJ_GFX_ID_A, VAR_0x800A)
           clearflag(FLAG_TEMP_B)
           clearflag(FLAG_TEMP_A)
           return
       case DAYCARE_ONE_MON:
           //setvar(VAR_OBJ_GFX_ID_A, VAR_0x800A)
           setflag(FLAG_TEMP_B)
           clearflag(FLAG_TEMP_A)
           return
    }   
}

//see daycare.c#updateDaycareGfxVars
script ISLANDGAME_FRESA_FARMS_Daycare_Mon1 {
    lock
    msgbox(ISLANDGAME_FRESA_FARMS_Daycare_Mon1_Text, MSGBOX_NPC)
    release
}

text ISLANDGAME_FRESA_FARMS_Daycare_Mon1_Text {
    format("{STR_VAR_1} looks happy!")
}

script ISLANDGAME_FRESA_FARMS_Daycare_Mon2 {
    lock
    msgbox(ISLANDGAME_FRESA_FARMS_Daycare_Mon2_Text, MSGBOX_NPC)
    release
}

text ISLANDGAME_FRESA_FARMS_Daycare_Mon2_Text {
    format("{STR_VAR_2} stares at you intently.")
}

script ISLANDGAME_FRESA_FARMS_OldMan_Request {
    lock
    applymovement(OLD_MAN_OW_ID, Common_Movement_FaceLeft)
    waitmovement(OLD_MAN_OW_ID)
    applymovement(OLD_MAN_OW_ID, Common_Movement_ExclamationMark)
    waitmovement(OLD_MAN_OW_ID)
    goto(ISLANDGAME_FRESA_FARMS_OldMan_Talk)
}

text ISLANDGAME_FRESA_FARMS_OldMan_RequestMessage {
    format(
        "Oh, its you!\p"
        "I couldn't help but notice your battle earlier.\p"
        "I was wondering if you could help us with our missing Mareep.\p"
        "She usually goes off by herself to the center of the plains, but we haven't seen her since."
    )
}

text ISLANDGAME_FRESA_FARMS_OldMan_RequestFulfilledMessage {
    format("{COLOR DARK_GRAY}Thank you very much!")
}

script ISLANDGAME_FRESA_FARMS_OldMan_Talk {
    lock
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case 0: // initial
            msgbox(ISLANDGAME_FRESA_FARMS_OldMan_RequestMessage)
            startquest(QUEST_FRESA_FARMS_LOST_MAREEP)
        case 4:
            msgbox(ISLANDGAME_FRESA_FARMS_OldMan_RequestFulfilledMessage)
        default:
            msgbox(ISLANDGAME_FRESA_FARMS_OldMan_RequestMessage)
    }
    release
}

script ISLANDGAME_FRESA_FARMS_Sign {
    msgbox(format("FRESA FARMS\nLeave your Pokémon to us!"), MSGBOX_SIGN)
}

script ISLANDGAME_FRESA_FARMS_Man {
    lock
    faceplayer
    msgbox(format("My parents established this farm a long time ago. One day, it'll belong to me.\pI hope I'll be able to run it just as well as they do."))
    release
}

script ISLANDGAME_FRESA_FARMS_Miltank_Interact {
    lock
    faceplayer
    playmoncry(SPECIES_MILTANK, CRY_MODE_NORMAL)
    msgbox(format("Moo. (Moo.)"))
    release
}

script ISLANDGAME_FRESA_FARMS_Chansey_Interact {
    lock
    faceplayer
    playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
    msgbox(format("Chansey. (Most humans can't understand us, but some are born with a special ability to comprehend what Pokémon are saying.)\pChansey. (You're one of them, aren't you?)"))
    release
}

script ISLANDGAME_FRESA_FARMS_Mareep1 {
    lock
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    msgbox(format("Reeep. (Heh. That dumb dog's snoozing on his job again!)\pReep reep. (Time to sneak out and have some fun with Chansey...)\pReep? (Huh?)"))
    faceplayer
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    waitmovement(0)
    msgbox(format("Reep... (Oh, it's just a random kid. Good thing they can't understand me.)\pReep! (Go away! I've got big plans for tonight!)\pReep. (Oh wait... They don't understand me.)"))
    applymovement(2, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_FRESA_FARMS_Mareep2 {
    lock
    faceplayer
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    msgbox(format("Reep. (I'm just saying man, if it was me against that Mightyena, hoo boy.)\pReep! (I would'a messed that Mightyena up real good, ya feel me?)\pReep? (Huh? Why are you looking me like that? I'm fo' realzies!)"))
    release
}

script ISLANDGAME_FRESA_FARMS_Mareep3 {
    lock
    faceplayer
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    if(!flag(FLAG_FRESA_FARMS_MAREEP_GIFT)) {
        msgbox(format("Reep! (Hey there! Thanks for saving my butt from that scary Mightyena.)\pReep. (Please take this as a gift.)"))
        checkitemspace(ITEM_ELECTRIC_GEM)
        if(!var(VAR_RESULT)) {
            msgbox(format("Reep. (Oh, you don't have enough space for this.)"))
        }
        else {
            setflag(FLAG_FRESA_FARMS_MAREEP_GIFT)
            giveitem(ITEM_ELECTRIC_GEM)
        }
    }
    else {
        msgbox(format("Reep. (I found it wandering around today. Hope it helps you out!)"))

    }
    release
}

script ISLANDGAME_FRESA_FARMS_Mareep4 {
    lock
    faceplayer
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    msgbox(format("Reep. (My little brother always strays away from the herd to explore.)\pReep. (Hopefully he's finally learned his lesson.)"))
    release
}

script ISLANDGAME_FRESA_FARMS_EventScripts_MilkVendor {
    msgbox(format("Welcome to Fresa Farms!\pOur produce is sourced from the healthiest Pokemon in Mariyama!"), MSGBOX_NPC)
    pokemart(MilkVendorItems)
    msgbox(format("Thank you for supporting us!"), MSGBOX_NPC)
}

mart MilkVendorItems {
    ITEM_MOOMOO_MILK
}