const LOOP_BOUND = VAR_TEMP_A
const LOOP_ITER  = VAR_TEMP_B
const ITEM_VAR   = VAR_TEMP_C

mapscripts ISLANDGAME_APPLEVINE_COUCH_POTATO_MapScripts {}


script ISLANDGAME_APPLEVINE_COUCH_POTATO_CouchPotatoSpeak {
    lock
    faceplayer
    returnqueststate(QUEST_APPLEVINE_COUCH_POTATO)

    switch (var(VAR_RESULT)) {
        case QUEST_INACTIVE:
            msgbox(format("I'm so hungry, but I'm relaxing on my couch....\p
                           Would you like to fetch food for me? Thanks."))
            startquest(QUEST_APPLEVINE_COUCH_POTATO)
            bufferitemname(STR_VAR_1, ITEM_HP_UP)
            msgbox(format("To start you off, I'm looking for an {STR_VAR_1}!"))
        case QUEST_ACTIVE:
            setvar(LOOP_ITER, SUB_QUEST_COUCH_POTATO_1)
            setvar(LOOP_BOUND, (SUB_QUEST_COUCH_POTATO_1 + SUB_QUEST_COUNT_COUCH_POTATO))
            call(Common_NextSubQuest)
            // -> {STR_VAR_1} = quest name
            // -> {STR_VAR_2} = item/pokemon name
            // -> var(VAR_RESULT) = the actual id
            // -> var(LOOP_ITER)  = the subquest that wasn't complete
            // -> var(LOOP_BOUND) = the index that represents the number of subquests
            copyvar(ITEM_VAR, VAR_RESULT)
            checkitem(ITEM_VAR)
            if (var(VAR_RESULT) == FALSE) { // don't have the item
                msgbox(format("Hey, what gives! Where's my {STR_VAR_2}?"))
                goto(CouchPotatoSpeakEnd)
            }
            removeitem(ITEM_VAR)
            msgbox(format("Looks like you got a {STR_VAR_2}."))
            subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_APPLEVINE_COUCH_POTATO, LOOP_ITER)
            addvar(LOOP_ITER, 1) // we add 1 here to see if finished the last quest (ie, we are next to the loop bound by index)
            if (var(LOOP_ITER) == LOOP_BOUND) { // finished the last quest
                msgbox(format("Hold on! I don't have any money to give you!\p
                               ...\p
                               What do you expect me to do then? Get a job?\p
                               I have an idea, just give me one second...\p"))
                giveitem(ITEM_LEFTOVERS)
                msgbox(format("What are you looking at? Be grateful that I even gave you any!"))
                completequest(QUEST_APPLEVINE_COUCH_POTATO)
            } else {
                showmoneybox(0,0)
                msgbox(format("You want my spare change? Fine, here you go."))
                call(ISLANDGAME_APPLEVINE_COUCH_POTATO_ItemReward)
                updatemoneybox
                msgbox(format("That's all you get."))
                hidemoneybox
            }
        case QUEST_COMPLETED:
            msgbox(format("If you want your money, I still don't have a job, okay?\p
                           It's a work in progress...."))
    }
    
    CouchPotatoSpeakEnd:
    release
}

script ISLANDGAME_APPLEVINE_COUCH_POTATO_Munchlax {
    lock
    faceplayer
    msgbox(format("You can't tell if this Munchlax weighs more than the guy on the couch."))
    playmoncry(SPECIES_MUNCHLAX, CRY_MODE_NORMAL)
    waitmoncry
    release
}

script ISLANDGAME_APPLEVINE_COUCH_POTATO_ItemReward {
    switch(var(ITEM_VAR)) {
        case ITEM_HP_UP:
            addmoney(100)
        case ITEM_PROTEIN: // not technically called
            addmoney(200)
    }
    return
}