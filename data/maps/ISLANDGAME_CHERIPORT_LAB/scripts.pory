//see moves.h
const I_MOVES_COUNT = 831
const SCIENTIST_ID = 3

mapscripts ISLANDGAME_CHERIPORT_LAB_MapScripts {}

// professor
script ISLANDGAME_CHERIPORT_LAB_EventScript_ProfTalk {
    lock
    faceplayer
    // the first time player interacts with prof
    if (!flag(FLAG_PROF_GAVE_EEVEE)) {
        msgbox(ProfTalk_First_Text)
        setflag(FLAG_PROF_GAVE_EEVEE)
        setflag(FLAG_SYS_POKEMON_GET)
        // givemon(SPECIES_EEVEE, 5, ITEM_NONE, ITEM_PREMIER_BALL, NUM_NATURES, 1, 0, 0, 0, 0, 0, 0, 32, 32, 32, 32, 32, 32, MOVE_TACKLE, MOVE_TAIL_WHIP, MOVE_SAND_ATTACK, MOVE_HELPING_HAND, FALSE)
        givemon(SPECIES_EEVEE,5,ITEM_NONE,ITEM_CHERISH_BALL,NUM_NATURES,1,,,,,,,,,,,,,,MOVE_TACKLE,MOVE_MUD_SLAP,MOVE_SAND_ATTACK,MOVE_TAIL_WHIP,FALSE)
        playfanfare(MUS_OBTAIN_ITEM)
        bufferspeciesname(STR_VAR_1, SPECIES_EEVEE)
        msgbox(ReceivedEevee_Text)
        waitfanfare 

        msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
        if (var(VAR_RESULT)) {
            //VAR_8004 is the parameter that the game uses to determine which mon is to be nicknamed
            //the getgiftmonpartyslot script calcs it for us, otherwise, we would have to explicitly set the parameter to 0 (?).
            call(Common_EventScript_GetGiftMonPartySlot)
            call(Common_EventScript_NameReceivedPartyMon)
        }
        msgbox(ProfTalk_Second_Text)
        // Give Pokedex
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(Prof_Recieved_Pokedex_Text)
        waitfanfare
        setflag(FLAG_SYS_POKEDEX_GET)
        special(SetUnlockedPokedexFlags)
        setflag(FLAG_SYS_NATIONAL_DEX)
        special(EnableNationalPokedex)
        setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
        
        msgbox(ProfTalk_Third_Text)
        // Give Pokeballs
        giveitem(ITEM_POKE_BALL, 5)
        msgbox(ProfTalk_Fourth_Text)
        setrespawn(HEAL_LOCATION_CHERIPORT)
        setflag(FLAG_BADGE01_GET) // temporary until trade disobedience fixed
    } 
    // Before player defeats Audrey for the first time.
    if(!flag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)) {
        msgbox(ProfTalk_AudreyNotDefeated)
    }
    // everything else; check how many Pokemon you've caught
    else {
        msgbox(ProfTalk_CheckPokedex)
    }
    
    release
}

text ProfTalk_First_Text {
    format(
        "{COLOR GREEN}Prof. Palm: {COLOR DARK_GRAY}{PLAYER}! It's a pleasure to finally meet you in person! \p"
        "{COLOR DARK_GRAY}How are you enjoying the isles so far? Hopefully the trip was smooth sailing. \p"
        "{COLOR DARK_GRAY}The weather will probably take some getting used to though. It's a lot more humid here than what you're used to, isn't it? \p"
        "{COLOR DARK_GRAY}Anyways, I bet you're itching to begin your adventure, so I'll save the small talk for later and present you with your very first companion!"
    )
}

text ProfTalk_Second_Text {
    format(
        "{COLOR GREEN}Prof. Palm: {COLOR DARK_GRAY}Haha, I see it's already taken a liking to you! Treat it with care and respect, and it'll do the same for you! \p"
        "{COLOR DARK_GRAY}Along with your new friend, I'll also be entrusting you with this Pokédex!"
    )
}

text Prof_Recieved_Pokedex_Text {
    format(
        "{COLOR DARK_GRAY}{PLAYER} recieved the Pokédex!"
    )
}

text ProfTalk_Third_Text {
    format(
        "{COLOR GREEN}Prof. Palm: {COLOR DARK_GRAY}The Pokédex is a highly advanced piece of tech that will automatically record the data of any Pokémon you see or catch, making it an essential piece of tech to have with you while you're conducting fieldwork!"
        "{COLOR DARK_GRAY}Wahaha, isn't the power of science just amazing? \p"
        "{COLOR DARK_GRAY}Oh, you'll also need some of these too!"
    )
}

text ProfTalk_Fourth_Text {
    format(
        "{COLOR GREEN}Prof. Palm: {COLOR DARK_GRAY}These are Pokéballs. They're a special tool that you can use to catch wild Pokémon! \p"
        "{COLOR DARK_GRAY}And with that, {PLAYER}, you're all set to begin your journey! \p"
        "{COLOR DARK_GRAY}I suggest you meet up with my other student first, though. \p"
        "{COLOR DARK_GRAY}Her name is Melissa, and she's been helping me out for about a week or two. \p"
        "{COLOR DARK_GRAY}She knows her way around Oranna better than me, so she can help you navigate the island and give you some pointers! \p"
        "{COLOR DARK_GRAY}Melissa should be somewhere near her hometown, Maplegrove, right now."
    )
}

text ProfTalk_AudreyNotDefeated {
    format("{COLOR DARK_GRAY}Melissa should be somewhere near her hometown, Maplegrove, right now.")
}

text ProfTalk_CheckPokedex {
    format(
        "{COLOR GREEN}Prof. Palm: {COLOR DARK_GRAY}{PLAYER}! It's good to see you again. How have you been? \p"
        "Let's see the progress you've made on completing your Pokédex... \p"
        "You've seen a total of {COLOR RED}(X) Pokémon... and caught a total of {COLOR RED}(Y) of them! \p"
        "Not too shabby at all, {PLAYER}! Keep up the good work!"
        )
}











script ISLANDGAME_CHERIPORT_LAB_EventScript_Scientist1Talk {
    lock
    faceplayer
    msgbox(Scientist1Talk_Text)
    release
}

script ISLANDGAME_CHERIPORT_LAB_EventScript_Scientist2Talk {
    lock
    msgbox(format("Heheh...{PAUSE 60} Yeah...{PAUSE 120} H-Huh?"))
    playse(SE_PIN)
    applymovement(SCIENTIST_ID, ExclaimFacePlayer)
    waitmovement(0)
    applymovement(SCIENTIST_ID, Common_Movement_Delay48)
    waitmovement(0)
    msgbox(format("I-It's not what you think! I was just doing research!"))
    applymovement(SCIENTIST_ID,Common_Movement_QuestionMark)
    waitmovement(0)
    applymovement(SCIENTIST_ID, Common_Movement_Delay48)
    waitmovement(0)
    msgbox(format("Huh? You're not the professor...{PAUSE 60} Go away! I'm doing grown-up stuff!"))
    applymovement(SCIENTIST_ID,Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_CHERIPORT_LAB_EventScript_CocoTalk {
    lock
    msgbox(CocoTalk_Text)
    release
}

text Scientist1Talk_Text {
    format(
        "{COLOR DARK_GRAY}You kids are lucky. While you're outside traveling and exploring, I'm stuck here. \p"
        "{COLOR DARK_GRAY}Sorting bookshelves, writing research papers, and programming software."
    )
}

text CocoTalk_Text {
    format(
        "{COLOR RED}(She seems preoccupied... I should leave her be.)"
    )
}

text ReceivedEevee_Text {
    format("{COLOR DARK_GRAY}{PLAYER} recieved an {STR_VAR_1}!")
}

script ISLANDGAME_CHERIPORT_LAB_EventScript_Vigoroth {
    lock
    msgbox(ISLANDGAME_CHERIPORT_LAB_VigorothTalk)
    release
}

text ISLANDGAME_CHERIPORT_LAB_VigorothTalk {
    format("{COLOR RED}(It looks like the Pokémon is hard at work.)")
}

script ISLANDGAME_CHERIPORT_LAB_EventScript_PikaDoll {
    lock
    msgbox(ISLANDGAME_CHERIPORT_LAB_PikaDollTalk)
    release
}

text ISLANDGAME_CHERIPORT_LAB_PikaDollTalk {
    format("{COLOR RED}(Wow! This stuffed Pikachu is a lot bigger than mine.)")
}
