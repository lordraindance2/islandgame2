const STATE_INITIAL = 0
const STATE_STARTED_QUEST = 1
const STATE_FOUND_BANDIT = 5
const STATE_DEFEATED_BANDIT = 2
const STATE_ARTHUR_FIGHT = 3
const STATE_FINAL = 4

const POS_HIDDEN_X = 0
const POS_HIDDEN_Y = 0

const LOCALID_ARTHUR_SISTER = 3
const LOCALID_ARTHUR_DAD = 7
const LOCALID_ARTHUR_MOM = 8
const LOCALID_ARTHUR = 6
const LOCALID_PUDDLETRAINER = 4
const LOCALID_PUDDLEMON = 5

mapscripts ISLANDGAME_ROSEVALE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch (var(VAR_ROSEVALE_STATE)) {
            case STATE_STARTED_QUEST:
                // only sister and dad blocking inn are visible
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 12, 14)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
            case STATE_DEFEATED_BANDIT:
                // everyone is standing around the inn
                setobjectxyperm(LOCALID_ARTHUR, 20, 25)
                setobjectxyperm(LOCALID_ARTHUR_DAD, 18, 24)
                setobjectxyperm(LOCALID_ARTHUR_MOM, 19, 24)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 20, 26)
                addobject(LOCALID_ARTHUR)
                addobject(LOCALID_ARTHUR_DAD)
                addobject(LOCALID_ARTHUR_MOM)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR, MOVEMENT_TYPE_FACE_LEFT)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
                turnobject(LOCALID_ARTHUR, DIR_WEST)
                turnobject(LOCALID_ARTHUR_SISTER, DIR_WEST)
                turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            case STATE_FINAL:
                // everyone is hidden, inn is open
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, POS_HIDDEN_X, POS_HIDDEN_Y)
            default:

        }

        if (var(VAR_ROSEVALE_STATE) != STATE_ARTHUR_FIGHT && var(VAR_ROSEVALE_STATE) != STATE_FINAL) {
                setobjectxyperm(LOCALID_ARTHUR_DAD, 17, 24)
                addobject(LOCALID_ARTHUR_DAD)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ROSEVALE_STATE, STATE_DEFEATED_BANDIT {
            setvar(VAR_ROSEVALE_STATE, STATE_ARTHUR_FIGHT)

            // everyone thanks you for helping
            msgbox(format("Arthurs Sister: You did it! My Marill!"), MSGBOX_NPC)
            msgbox(format("Arthurs Mom: Thank you for helping like you did today.\nThis means a lot to us."), MSGBOX_NPC)
            msgbox(format("Arthurs Sister: Hey, take this for helping us out!"), MSGBOX_NPC)
            giveitem(ITEM_AMULET_COIN)
            msgbox(format("Arthurs Dad: Please, stay the night to rest and recover.\nI'm sure you both have had a long day."), MSGBOX_NPC)

            //warp
            special(HealPlayerParty)
            setrespawn(HEAL_LOCATION_ISLANDGAME_ROSEVALE) // so you can't skip arthur if you lose
            warp(MAP_ISLANDGAME_ROSEVALE_INN, 5, 3)
        }
    ]
}

script ISLANDGAME_ROSEVALE_Interact_LockedDoor {
    msgbox(format("The door is locked"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_PuddleMon {
    lock
    faceplayer
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_ENCOUNTER)
    waitmoncry
    release
}

movement Movement_TrainerJump {
    jump_in_place_left
    jump_in_place_up
    jump_in_place_right
    jump_in_place_down
    jump_in_place_left
}

movement Movement_MonWatch {
    face_right
}

movement Movement_MonJump {
    jump_in_place_right
}

script ISLANDGAME_ROSEVALE_Interact_PuddleTrainer {
    lock
    msgbox(format("It's ok! The water isn't going to hurt you...\nWatch!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonWatch)
    applymovement(LOCALID_PUDDLETRAINER, Movement_TrainerJump)
    waitmovement(0)
    msgbox(format("See?\nIt's shallow!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonJump)
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_NORMAL)
    waitmoncry
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_EntranceSign {
    msgbox(format("Welcome to Rosevale!"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_InnSign {
    msgbox(format("ROSEVALE INN"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurSister {
    msgbox(format("I'm scared. Is everything going to be ok?"), MSGBOX_NPC)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurDad {
    msgbox(format("Sorry, the Inn is closed right now..."), MSGBOX_NPC)
}

// section: sister cutscene pre-aurora
text Text_ArthurSister_PleaseHelp {
    format(
        "Excuse me, I need your help!\p"
        "A thief stole my Marill and down this path towards Mt. Aurora!\p"
        "My brother chased after them, but I don't think he can fight...\p"
        "Can you bring them back safely?"
    )
}

movement Movement_ArthurSister_RunUpToPath {
    walk_fast_left * 6
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveWithoutQuest {
    lock

    // the sister runs up
    addobject(LOCALID_ARTHUR_SISTER)
    setobjectxy(LOCALID_ARTHUR_SISTER, 18, 14)
    applymovement(LOCALID_ARTHUR_SISTER, Movement_ArthurSister_RunUpToPath)
    waitmovement(LOCALID_ARTHUR_SISTER)

    // she asks if you can help
    msgbox(Text_ArthurSister_PleaseHelp, MSGBOX_NPC)

    // quest starts
    setvar(VAR_ROSEVALE_STATE, STATE_STARTED_QUEST)

    release
}

// section: arthur fight
movement Movement_Arthur_WalkUpToExit {
    walk_right * 7
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveArthurFight {
    lock
    // arthur walks up
    addobject(LOCALID_ARTHUR)
    setobjectxy(LOCALID_ARTHUR, 25, 12)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkUpToExit)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    waitmovement(0)

    // arthur explains situation
    msgbox(format(
        "My dad won't let me join you in the festival tournament.\p"
        "He doesn't think I'm ready yet...\p"
        "But he's wrong! I am ready!"
    ), MSGBOX_NPC)

    // battle starts
    trainerbattle_single(TRAINER_SPECIAL_ARTHUR_1, format("Let me prove it, right now!"), "Am I not good enough?", ArthurDefeated)
    release
}

movement Movement_ArthurDad_WalkUpToArthur {
    walk_right * 6
}

movement Movement_Arthur_SeesDad {
    delay_16 * 4
    face_left
    emote_exclamation_mark
}

movement Movement_ArthurDad_WalkAway {
    walk_left * 7
}

movement Movement_Arthur_WalkAway {
    delay_16 * 4
    walk_left * 8
}

script(local) ArthurDefeated {
    // arthur talks about fight
    msgbox(format(
        "I think my dad was right...\p"
        "Maybe I'm just not cut out for battling."
    ), MSGBOX_NPC)

    // dad walks up
    addobject(LOCALID_ARTHUR_DAD)
    setobjectxy(LOCALID_ARTHUR_DAD, 25, 12)
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkUpToArthur)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_SeesDad)
    waitmovement(LOCALID_ARTHUR_DAD)
    waitmovement(LOCALID_ARTHUR)

    // messages between dad, arthur, player
    msgbox(format(
        "Arthurs Dad: No, Arthur, that's not true.\p"
        "Arthurs Dad: Even though you weren't ready yet, you have a lot of potential.\p"
        "Arthurs Dad: Keep training! If you can beat Ruka at Tidalcove, I'll let you go."
    ), MSGBOX_NPC)
    turnobject(LOCALID_ARTHUR, DIR_EAST)
    msgbox(format(
        "Arthur: Did you hear that? I definitely meet you again for the tournament!\p"
        "Arthur: Ruka might be really tough, but I know I can do it.\p"
        "Arthur: Good luck with your challenge!"
    ), MSGBOX_NPC)

    // dad and arthur walk away
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkAway)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkAway)
    waitmovement(LOCALID_ARTHUR)
    waitmovement(LOCALID_ARTHUR_DAD)
    removeobject(LOCALID_ARTHUR)
    removeobject(LOCALID_ARTHUR_DAD)

    setvar(VAR_ROSEVALE_STATE, STATE_FINAL)
    release
}