const STATE_INITIAL = 0
const STATE_STARTED_QUEST = 1
const STATE_FOUND_BANDIT = 5
const STATE_DEFEATED_BANDIT = 2
const STATE_ARTHUR_FIGHT = 3
const STATE_FINAL = 4

const POS_HIDDEN_X = 0
const POS_HIDDEN_Y = 0

const LOCALID_ARTHUR_SISTER = 1
const LOCALID_ARTHUR_DAD = 5
const LOCALID_ARTHUR_MOM = 6
const LOCALID_ARTHUR = 4
const LOCALID_PUDDLETRAINER = 2
const LOCALID_PUDDLEMON = 3

mapscripts ISLANDGAME_ROSEVALE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch (var(VAR_ROSEVALE_STATE)) {
            case STATE_STARTED_QUEST:
                // only sister and dad blocking inn are visible
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 14, 14)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
            case STATE_DEFEATED_BANDIT:
                // everyone is standing around the inn
                setobjectxyperm(LOCALID_ARTHUR, 20, 25)
                setobjectxyperm(LOCALID_ARTHUR_DAD, 18, 24)
                setobjectxyperm(LOCALID_ARTHUR_MOM, 19, 24)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 20, 26)
                addobject(LOCALID_ARTHUR)
                addobject(LOCALID_ARTHUR_DAD)
                addobject(LOCALID_ARTHUR_MOM)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR, MOVEMENT_TYPE_FACE_LEFT)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
                turnobject(LOCALID_ARTHUR, DIR_WEST)
                turnobject(LOCALID_ARTHUR_SISTER, DIR_WEST)
                turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            case STATE_FINAL:
                // everyone is hidden, inn is open
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, POS_HIDDEN_X, POS_HIDDEN_Y)
            case STATE_FOUND_BANDIT:
                // only sister and dad blocking inn are visible
                setobjectxyperm(LOCALID_ARTHUR, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_MOM, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_DAD, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ARTHUR_SISTER, 14, 14)
                addobject(LOCALID_ARTHUR_SISTER)
                setobjectmovementtype(LOCALID_ARTHUR_SISTER, MOVEMENT_TYPE_FACE_LEFT)
            default:

        }

        if (var(VAR_ROSEVALE_STATE) != STATE_ARTHUR_FIGHT && var(VAR_ROSEVALE_STATE) != STATE_FINAL) {
                setobjectxyperm(LOCALID_ARTHUR_DAD, 19, 24)
                setobjectmovementtype(LOCALID_ARTHUR_DAD, MOVEMENT_TYPE_FACE_LEFT_AND_RIGHT)
                addobject(LOCALID_ARTHUR_DAD)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ROSEVALE_STATE, STATE_DEFEATED_BANDIT {
            setvar(VAR_ROSEVALE_STATE, STATE_ARTHUR_FIGHT)

            // everyone thanks you for helping
            msgbox(format("{COLOR GREEN}Arthur's Sister: {COLOR DARK_GRAY}Yay! Fank you for getting Marill back!"), MSGBOX_NPC)
            msgbox(format("{COLOR DARK_GRAY}Here! You can have this."), MSGBOX_NPC)
            giveitem(ITEM_AMULET_COIN)
            msgbox(format("{COLOR GREEN}Arthur's Mom: {COLOR DARK_GRAY}How can we ever repay you, {PLAYER}?"), MSGBOX_NPC)
            msgbox(format("{COLOR GREEN}Arthur's Mom: {COLOR DARK_GRAY}Please, stay the night at our inn. It's the least we can do."), MSGBOX_NPC)

            //warp
            special(HealPlayerParty)
            setrespawn(HEAL_LOCATION_ISLANDGAME_ROSEVALE) // so you can't skip arthur if you lose
            warp(MAP_ISLANDGAME_ROSEVALE_INN, 5, 3)
        }
    ]
}


script ISLANDGAME_ROSEVALE_Interact_PuddleMon {
    lock
    faceplayer
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_ENCOUNTER)
    msgbox(format("Char... {COLOR BLUE}(save me from this purgatory)"))
    release
}

movement Movement_TrainerJump {
    jump_in_place_left
    jump_in_place_up
    jump_in_place_right
    jump_in_place_down
    jump_in_place_left
}

movement Movement_MonWatch {
    face_right
}

movement Movement_MonJump {
    jump_in_place_right
}

script ISLANDGAME_ROSEVALE_Interact_PuddleTrainer {
    lock
    msgbox(format("It's ok! The water isn't going to hurt you...\nWatch!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonWatch)
    applymovement(LOCALID_PUDDLETRAINER, Movement_TrainerJump)
    waitmovement(0)
    msgbox(format("See?\nIt's shallow!"))
    closemessage
    applymovement(LOCALID_PUDDLEMON, Movement_MonJump)
    playmoncry(SPECIES_CHARMANDER, CRY_MODE_WEAK)
    waitmoncry
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC1 {
    lock
    faceplayer
    msgbox(format(
        "The air here is nice and fresh thanks to our Isle Guardian, Articuno.\p"
        "I'm glad I moved here from Tidalcove. The city life just isn't for me."
        )
    )
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC2 {
    lock
    msgbox(format(
        "Brrr... The winds are always so chilly here.\p"
        "Talonflame! Warm up my hands, will ya? I hate being on watch duty..."
        )
    )
    release
}

script ISLANDGAME_ROSEVALE_Interact_Talonflame {
    lock
    faceplayer
    playmoncry(SPECIES_TALONFLAME, CRY_MODE_NORMAL)
    msgbox(format("Talooon!"))
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC3 {
    lock
    faceplayer
    msgbox(format(
        "La dee dum... I love the sweet fragrance of flowers!"
        )
    )
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script ISLANDGAME_ROSEVALE_Interact_NPC4 {
    lock
    faceplayer
    msgbox(format("My gramma said I needed to “touch grass” more, so here I am."))
    release
}

script ISLANDGAME_ROSEVALE_Interact_EntranceSign {
    msgbox(format("ROSEVALE\nThe village under the alps"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_InnSign {
    msgbox(format("PEACHY SWEET INN\nWe're positively peachy keen!\n"), MSGBOX_SIGN)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurSister {
    msgbox(format("My bwother went that-a-way. I think he went into the mountain!"), MSGBOX_NPC)
}

script ISLANDGAME_ROSEVALE_Interact_ArthurDad {
    msgbox(format("Where in the world could that boy be...?"), MSGBOX_NPC)
}



// section: sister cutscene pre-aurora
text Text_ArthurSister_PleaseHelp {
    format(
        "You're a twainer, right? Someone stole my A-zoo-ril! \p"
        "My bwother Arfur ran after the thief, but he still hasn't come back!\p"
        "He told me not to worry or tell Mom or Dad... but I'm getting worried!\p"
        "I don't wanna tell Mom and Dad. They'll be weally mad at him if they find out...\p"
        "Pwease help me find him and get my A-zoo-ril back!"
    )
}

movement Movement_ArthurSister_RunUpToPath {
    walk_fast_left * 6
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveWithoutQuest {
    lock
    msgbox(format("Hewwo, I need your help!"))
    closemessage
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    // the sister runs up
    addobject(LOCALID_ARTHUR_SISTER)
    setobjectxy(LOCALID_ARTHUR_SISTER, 20, 14)
    applymovement(LOCALID_ARTHUR_SISTER, Movement_ArthurSister_RunUpToPath)
    waitmovement(LOCALID_ARTHUR_SISTER)

    // she asks if you can help
    msgbox(Text_ArthurSister_PleaseHelp)

    // quest starts
    setvar(VAR_ROSEVALE_STATE, STATE_STARTED_QUEST)

    release
}

// section: arthur fight
movement Movement_Arthur_WalkUpToExit {
    walk_right * 7
}

script ISLANDGAME_ROSEVALE_Trigger_TryLeaveArthurFight {
    lock
    // arthur walks up
    msgbox(format("{COLOR GREEN}Arthur: {COLOR DARK_GRAY}{PLAYER}!"))
    addobject(LOCALID_ARTHUR)
    setobjectxy(LOCALID_ARTHUR, 25, 12)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkUpToExit)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    waitmovement(0)

    // arthur explains situation
    msgbox(format(
        "Before you go, I... I... \p"
        "I want to challenge you to a Pokémon battle! \p" 
        "I-I couldn't do anything against that thief... If it wasn't for you and Articuno... My little sister's Pokémon would've been lost forever! \p"
        "I need to get stronger, so when the time comes, I'll be able to protect her!\p"
        "I wanna be just like you, and take on the Mariyama Gym Challenge to hone my skills!\p"
        "But Dad says I'm not ready yet... \p"
    ), MSGBOX_NPC)

    // battle starts
    trainerbattle_single(TRAINER_SPECIAL_ARTHUR_1, format("So I'll prove it to him! Right here and now! Prepare yourself, {PLAYER}!"), "...\pI lost again...\pWhy do I keep losing?", ArthurDefeated)
    release
}

movement Movement_ArthurDad_WalkUpToArthur {
    walk_right * 6
}

movement Movement_Arthur_SeesDad {
    delay_16 * 4
    face_left
    emote_exclamation_mark
}

movement Movement_ArthurDad_WalkAway {
    walk_left * 7
}

movement Movement_Arthur_WalkAway {
    delay_16 * 4
    walk_left * 8
}

script(local) ArthurDefeated {
    // arthur talks about fight
    msgbox(format(
        "{COLOR GREEN}Arthur: {COLOR DARK_GRAY}...\p"
        "{COLOR DARK_GRAY}Heh... You're a great trainer, {PLAYER}. You can definitely take on Ruka. \p"
        "{COLOR DARK_GRAY}Me on the other hand? I should just give up.\p"
        "{COLOR DARK_GRAY}I'm not cut out to be a Pokémon trainer. I'm a failure. \p"
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}No, Arthur, that's not true.\p"
    ), MSGBOX_NPC)

    // dad walks up
    addobject(LOCALID_ARTHUR_DAD)
    setobjectxy(LOCALID_ARTHUR_DAD, 25, 12)
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkUpToArthur)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_SeesDad)
    waitmovement(LOCALID_ARTHUR_DAD)
    waitmovement(LOCALID_ARTHUR)

    // messages between dad, arthur, player
    msgbox(format(
        "Just because you haven't been successful with your first few battles, doesn't mean you're a failure. \p"
        "I watched your battle with {PLAYER}. The bond you share with your Pokémon is strong, and shows me that do you have potential. \p"
        "But you need more time. You can't expect to be a good trainer without putting in the effort. \p"
        "Listen, if you spend the next week training hard with your Pokémon, I'll let you challenge Ruka, alright?\p"
        "{COLOR GREEN}Arthur: -sniff- Okay."
        "{COLOR GREEN}Arthur's Dad: {COLOR DARK_GRAY}Good luck on the rest of your journey, {PLAYER}."
        "{COLOR DARK_GRAY}Our door will always be open to you."
    ), MSGBOX_NPC)
    
    // dad and arthur walk away
    applymovement(LOCALID_ARTHUR_DAD, Movement_ArthurDad_WalkAway)
    applymovement(LOCALID_ARTHUR, Movement_Arthur_WalkAway)
    waitmovement(LOCALID_ARTHUR)
    waitmovement(LOCALID_ARTHUR_DAD)
    removeobject(LOCALID_ARTHUR)
    removeobject(LOCALID_ARTHUR_DAD)

    setvar(VAR_ROSEVALE_STATE, STATE_FINAL)
    release
}