mapscripts ISLANDGAME_LUMINE_CAVE_B1F_2_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        if(flag(FLAG_DAILY_LUMINE_CAVE_SALT_4)) {
            setmetatile(11, 19, METATILE_Cave_ShoalCave_DirtPile_Small, FALSE)
        }
    }
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_SALT {
    lock
    if(!flag(FLAG_DAILY_LUMINE_CAVE_SALT_4)) {
        giveitem(ITEM_SHOAL_SALT)
        setmetatile(11, 19, METATILE_Cave_ShoalCave_DirtPile_Small, FALSE)
        special(DrawWholeMapView)
        setflag(FLAG_DAILY_LUMINE_CAVE_SALT_4)
    }
    else {
        msgbox(format(
            "{COLOR RED}(No more salt here.)"
        ))
        closemessage
    }
    release
}



// Main Quest Event

script ISLANDGAME_LUMINE_CAVE_B1F_2_PorygonInteract {
    lock
    faceplayer
    playmoncry(SPECIES_PORYGON2, CRY_MODE_NORMAL)
    msgbox(format(
        "I AM DESIGNATED AS PORYGON: DEFINITIVE EDITION REMASTERED!\p"
        "WHAT'S QUACKALACKIN'?"
    ))
    applymovement(6, Common_Movement_FaceRight)
    waitmovement(0)
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_AlbaInteract {
    lock
    if(!flag(FLAG_LUMINE_CAVE_SPOKE_TO_ALBA)) { // initial interaction
        playbgm(MUS_DP_ENCOUNTER_SUSPICIOUS, FALSE)
        msgbox(format(
            "Fascinating... the sheer ferocity of an Aggron is truly without parallel...\p"
            "Their adamantine iron hide that serves as both sword and shield, compounded with their instinctive territorial nature...\p"
            "This mass outbreak has proved quite beneficial in realizing the untapped potential of these Pokémon...\p"
            "Oh?"
        ))
        closemessage
        applymovement(1, Common_Movement_FaceLeft)
        waitmovement(0)
        applymovement(1, Common_Movement_QuestionMark)
        waitmovement(0)
        applymovement(1, Common_Movement_Delay48)
        waitmovement(0)
        msgbox(format(
            "Ah,{PAUSE 60} please excuse my ramblings, trainer. I was simply enamored by the majesty of the Aron family.\p"
            "I do advise that you keep a safe distance, however.\p"
            "They have claimed this cavern as their home, and will not show mercy to those who dare trespass it.\p"
            "It is in everyone's best interest to stay put until the authorities arrive."
        ))
        closemessage

        applymovement(22, Common_Movement_QuestionMark)
        waitmovement(0)
        applymovement(22, Common_Movement_WalkInPlaceFasterRight)
        waitmovement(0)
        applymovement(22, Common_Movement_Delay48)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
        waitmovement(0)
        msgbox(format(
            "Eeeh? But my poor little Litten is getting picked on by those meanies!\p"
            "Please, can't you save her?"
        ))
        closemessage

        applymovement(1, Common_Movement_WalkInPlaceLeft)
        waitmovement(0)
        applymovement(1, Common_Movement_Delay48)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
        waitmovement(0)
        msgbox(format(
            "I understand your frustrations, but it is too dangerous for any of us to tread onto their territory.\p"
            "Even I do not possess the capabilities to surmount a mighty Aggron...\p"
            "All we can do is hope for the best and await the Silverwing Order."
        ))
        closemessage

        applymovement(22, Common_Movement_WalkInPlaceRight)
        waitmovement(0)
        applymovement(22, Common_Movement_Delay48)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
        waitmovement(0)
        msgbox(format(
            "B-But!..."
        ))
        closemessage
        
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
        waitmovement(0)    
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
        waitmovement(0)
        msgbox(format(
            "You still wish to pass to rescue her Litten, despite my warnings?\p"
            "Hm... I in good faith cannot send you in there and risk your own livelihood...\p"
            "But if you are that confident in your abilities, I would like to verify them first in a battle.\p"
            "If you can best me, I will grant you passage to save her Pokémon.\p"
            "I will give you a moment to prepare. Speak to me when you are ready."
        ))
        setflag(FLAG_LUMINE_CAVE_SPOKE_TO_ALBA)
        fadedefaultbgm
    }
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_LittenGirl {
    lock
    faceplayer
    if (flag(FLAG_LUMINE_CAVE_SPOKE_TO_ALBA)) {
        msgbox(format(
            "Please save my litty kitty! D:"
        ))
    }
    else {
        msgbox(format(
            "Waaaah!{PAUSE 30} My Litten is trapped over there with those big bad bullies!"
        ))
    }
    applymovement(22, Common_Movement_FaceRight)
    waitmovement(0)
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC1 {
    lock
    msgbox(format(
        "See that big strong Aggron, buddy? That's what you're gonna grow up to be!"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_Aron {
    lock
    faceplayer
    playmoncry(SPECIES_ARON, CRY_MODE_NORMAL)
    msgbox(format(
        "Ron... {COLOR BLUE}(I'm glad to be owned by a trainer. The wild Pokémon world is scary.)"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_Swampert {
    lock
    playmoncry(SPECIES_SWAMPERT, CRY_MODE_WEAK)
    msgbox(format(
        "{COLOR RED}(Looks like Swampert is out of commission.)"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC2 {
    lock
    faceplayer
    msgbox(format(
        "The horns of an Aggron are strong enough to pierce steel with ease, while their claws can shred through the strongest of bedrock.\p"
        "It's believed that ancient generals used to wear armor made from the plating of an Aggron as a symbol of strength."
    ))
    closemessage
    release    
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC3 {
    lock
    faceplayer
    msgbox(format(
        "I could easily take down an Aggron by myself.{PAUSE 60} Just give me enough prep time!"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC4 {
    lock
    msgbox(format(
        "Swampert! Are you alright?\p"
        "Argh... We couldn't even put a dent on that Aggron...\p"
        "It doesn't add up. Why weren't water or ground moves super effective on it...?"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC5 {
    lock
    faceplayer
    msgbox(format(
        "Heck... I'm gonna miss out on meeting STARLIGHT cause of this DUMB Aggron!"
    ))
    closemessage
    release
}

script ISLANDGAME_LUMINE_CAVE_B1F_2_NPC6 {
    lock
    if(flag(FLAG_DAILY_LUMINE_CAVE_SALT_4)) {
        msgbox(format(
            "Eureka! A deposit of Sodium Chloride! This will go great with my french fries!"
        ))
    }
    else {
        faceplayer
        msgbox(format(
            "Huh? What do you mean, this isn't the same salt you put on food?"
        ))
    }
    closemessage
    release
}