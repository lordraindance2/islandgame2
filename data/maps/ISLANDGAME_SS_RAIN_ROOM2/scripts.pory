mapscripts ISLANDGAME_SS_RAIN_ROOM2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (!flag(FLAG_SET_WALL_CLOCK)) {
            setvar(VAR_TEMP_1, 7)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 7: SS_Rain_EventScript_SetClock
    ]
}
script SS_Rain_EventScript_NeedRunningShoes {
    if (!flag(FLAG_RECEIVED_RUNNING_SHOES)) { # Run trigger script if you have not recieved the running shoes
        lockall
        msgbox(NeedRunningShoes_Text)
        waitmessage
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, NeedRunningShoes_Movement)
        waitmovement(0)
    }
    releaseall
}

script SS_Rain_EventScript_SetClock {
    lockall
    setvar(VAR_TEMP_1, 3)
    fadescreen(FADE_TO_BLACK)
    msgbox(ArrivedAtCheriport_Text)
    waitmessage
	special(StartWallClock)
	waitstate
    delay(30)
    setflag(FLAG_SET_WALL_CLOCK)
    releaseall
}

script SS_Rain_EventScript_ItemRunningShoes {
    lockall
    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    msgbox(SwitchedRunningShoes_Text)
    removeobject(VAR_LAST_TALKED)
    setflag(FLAG_SYS_B_DASH)
    releaseall
}

movement NeedRunningShoes_Movement {
    walk_down
    face_down
}

text NeedRunningShoes_Text {
    "{COLOR DARK_GRAY}I should change into my Running Shoes first."
}

text SwitchedRunningShoes_Text {
    "{COLOR DARK_GRAY}You switched your shoes with the\n"
    "{COLOR DARK_GRAY}Running Shoes!"
}

text ArrivedAtCheriport_Text {
    format(
        "{COLOR DARK_GRAY}Ladies and gentleman, this is your captain speaking! \p"
        "{COLOR DARK_GRAY}I'm happy to announce to you that we've finally reached our destination! \p"
        "{COLOR DARK_GRAY}On behalf of the entire crew, I'd like to thank you for choosing to sail with us! \p"
        "{COLOR DARK_GRAY}Now, set forth and explore the isles of Mariyama! Until next time. Buh-bye! \p"
        "{COLOR RED}(I've finally arrived. I should set my watch to the correct time first...)"
    )
}