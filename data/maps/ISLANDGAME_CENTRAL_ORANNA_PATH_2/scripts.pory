const VAR_CUTSCENE_FINISHED = VAR_TEMP_0

const TRAINER_CORANNA_2_CAMPER1 = TRAINER_LOLA_1
const TRAINER_CORANNA_2_MANIAC = TRAINER_LOLA_2
const TRAINER_CORANNA_2_BIKER = TRAINER_LOLA_3
const TRAINER_CORANNA_2_FISHERMAN = TRAINER_LOLA_4

const LOCALID_LUXRAY = 8
const LOCALID_PRIM = 1
const LOCALID_FAN_1 = 7
const LOCALID_FAN_2 = 5

mapscripts ISLANDGAME_CENTRAL_ORANNA_PATH_2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_NORANNA_PRIM_CUTSCENE_FINISHED)) {
            setvar(VAR_CUTSCENE_FINISHED, 1)
        }
    }
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_SwimKid {
    msgbox(format("Man, swimming looks so fun!"), MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_HalfwayCamper {
    msgbox(format("Hey traveler! Nice job on making it this far.\pWe're at about halfway to Tidalcove.\nThis is a great place to hone your skills, especially before meeting Oranna's Gym Leader, Ruka!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Fisherman {
    trainerbattle_single(TRAINER_CORANNA_2_FISHERMAN, 
        format("Oh, are you interesting in seeing some of my catches?"), 
        format("Back to fishing.")
    )
    msgbox(format("This is a nice place to fish since it's so remote.\pNothing like waking up early to the sounds of nature and fishing!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Camper1 {
    trainerbattle_single(TRAINER_CORANNA_2_CAMPER1, 
        format("Isn't it nice for someone to put a resting-house in the middle of the woods?"), 
        format("Good thing for the resting house!")
    )
    msgbox(format("Camping is nice and all, but nothing beats a warm bed."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Maniac {
    trainerbattle_single(TRAINER_CORANNA_2_MANIAC, 
        format("Hehe... let's battle!"), 
        format("Impossible!")
    )
    msgbox(format("Listen: wild routes like this attract a lot of bug catchers and grass-type enthusiasts. Is it so wrong to bring a little heat?"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Biker {
    trainerbattle_single(TRAINER_CORANNA_2_BIKER, 
        format("Hey, check out my new ride! I love going fast!"), 
        format("How did you outspeed me?")
    )
    msgbox(format("Like, you should totally get a bike! The guy in Tidalcove is the best!"), MSGBOX_NPC)
}
 
movement Movement_LuxrayAttack {
    walk_right
}

movement Movement_PrimWalkAway {
    walk_right * 3
    walk_down
    walk_right * 4
}

movement Movement_LuxrayWalkAway {
    walk_down
    face_right
    delay_16 * 7
    walk_right * 5
}

movement Movement_Fan1WalkAway {
    delay_16 * 3
    walk_down * 2
    walk_right * 6
}

movement Movement_Fan2WalkAway {
    delay_16 * 10
    walk_up * 2
    walk_right * 3
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Trigger_PrimFightCutscene {
    lock

    // prim commands luxray to finish it
    msgbox(format("Prim: Luxray, finish things up here."))
    closemessage

    // luxray attacks
    applymovement(LOCALID_LUXRAY, Movement_LuxrayAttack)
    waitmovement(LOCALID_LUXRAY)
    playmoncry(SPECIES_LUXRAY, CRY_MODE_NORMAL)
    waitmoncry

    // prim roasts the opponent for losing
    msgbox(format("Prim: Man, that was a waste of time! You should have just given me the spot..."))
    closemessage

    // prim and 2 spectators walk away
    applymovement(LOCALID_PRIM, Movement_PrimWalkAway)
    applymovement(LOCALID_LUXRAY, Movement_LuxrayWalkAway)
    applymovement(LOCALID_FAN_1, Movement_Fan1WalkAway)
    applymovement(LOCALID_FAN_2, Movement_Fan2WalkAway)
    waitmovement(LOCALID_PRIM)
    waitmovement(LOCALID_LUXRAY)
    waitmovement(LOCALID_FAN_1)
    waitmovement(LOCALID_FAN_2)
    removeobject(LOCALID_PRIM)
    removeobject(LOCALID_LUXRAY)
    removeobject(LOCALID_FAN_1)
    removeobject(LOCALID_FAN_2)

    setflag(FLAG_NORANNA_PRIM_CUTSCENE_FINISHED)
    setvar(VAR_CUTSCENE_FINISHED, 1)
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightLoser {
    msgbox(format("Man, I lost so bad..."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec1 {
    msgbox(format("Prim is really good... if only she could wake up earlier."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec2 {
    msgbox(format("It doesn't matter how good Prim is; she still has no chance against Ruka."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec3 {
    msgbox(format(
        "Since so many people come by to challenge Ruka,"
        "she can only accept a limited amount of people each day."
        "Prim was just fighting for the last spot."
    ), MSGBOX_NPC)
}