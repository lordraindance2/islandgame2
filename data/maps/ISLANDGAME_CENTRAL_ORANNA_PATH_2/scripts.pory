const VAR_CUTSCENE_FINISHED = VAR_TEMP_0

const LOCALID_LUXRAY = 8
const LOCALID_PRIM = 1
const LOCALID_FAN_1 = 7
const LOCALID_FAN_2 = 4
const LOCALID_FAN_3 = 3
const LOCALID_FAN_4 = 5

mapscripts ISLANDGAME_CENTRAL_ORANNA_PATH_2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (flag(FLAG_NORANNA_PRIM_CUTSCENE_FINISHED)) {
            setvar(VAR_CUTSCENE_FINISHED, 1)
        }
    }
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_SwimKid {
    msgbox(format("Swimming looks so fun!"), MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_HalfwayCamper {
    lock
    faceplayer
    msgbox(
        format(
            "Howdy trainer! Nice job on making it this far. You're 'bout halfway there!\p"
            "For getting this far, I'll let you in on some insider knowledge...\p"
            "Ruka, the leader of Tidalcove's Gym, specializes in ice-type Pokémon!\p"
            "So fighting, fire, steel, and rock moves will be super useful in the fight!\p"
            "Bet you're glad I filled you in on the secrets, huh?\p"
            "Eh? What do you mean, that was all public information?\p"
        ), 
    MSGBOX_NPC)
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Fisherman {
    trainerbattle_single(TRAINER_CORANNA_2_FISHERMAN, 
        format("Interested in seeing some of my catches?"), 
        "Back to fishing."
    )
    msgbox(format("This is a nice place to fish since it's so remote.\pNothing like waking up early to the sounds of nature and fishing!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Camper1 {
    trainerbattle_single(TRAINER_CORANNA_2_CAMPER, 
        format("I sure am glad there's a rest stop in the middle of this path!\pI'm all rested up now, so let's battle!"), 
        "Darn it, you still beat me."
    )
    msgbox(format("Camping is nice and all, but nothing beats a warm bed..."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Maniac {
    trainerbattle_single(TRAINER_CORANNA_2_MANIAC, 
        format("Hehe... let's battle!"), 
        "Impossible!"
    )
    msgbox(format("Listen. Wild routes like this attract a lot of bug catchers and grass-type enthusiasts. Is it so wrong to bring a little heat?"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Battle_Biker {
    trainerbattle_single(TRAINER_CORANNA_2_BIKER, 
        format("Hey, check out my new ride! I love going fast!"), 
        "How did you outspeed me?"
    )
    msgbox(format("Like, you should totally get a bike! The guy in Tidalcove is the best!"), MSGBOX_NPC)
}
 
movement Movement_LuxrayAttack {
    jump_right
}

movement Movement_PrimWalkAway {
    walk_right * 4
    walk_down
    walk_right * 4
}

movement Movement_LuxrayWalkAway {
    walk_down
    face_right
    delay_16 * 7
    walk_right * 5
}

movement Movement_Fan1WalkAway {
    delay_16 * 3
    walk_down * 2
    walk_right * 6
}

movement Movement_Fan2WalkAway {
    delay_16 * 10
    walk_up * 2
    walk_right * 3
}

movement Movement_Fan3WalkAway {
    walk_fast_down
    walk_fast_right * 4
}

movement Movement_Fan4WalkAway {
    walk_fast_down * 2
    walk_fast_right * 4
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Trigger_PrimFightCutscene {
    lock

    // prim commands luxray to finish it
    msgbox(
        format(
            "{COLOR GREEN}???: {COLOR DARK_GRAY}Time to end this, Delcatty!"
            )
        )
    closemessage

    // luxray attacks
    applymovement(LOCALID_LUXRAY, Movement_LuxrayAttack)
    waitmovement(LOCALID_LUXRAY)
    playmoncry(SPECIES_DELCATTY, CRY_MODE_NORMAL)
    waitmoncry
    playmoncry(SPECIES_LOMBRE, CRY_MODE_WEAK)
    waitmoncry

    // prim roasts the opponent for losing
    msgbox(
        format("{COLOR GREEN}???: {COLOR DARK_GRAY}-sigh- What a waste of time. You should've just given me the spot.")
        )
    closemessage

    // prim and 2 spectators walk away
    applymovement(LOCALID_PRIM, Movement_PrimWalkAway)
    applymovement(LOCALID_LUXRAY, Movement_LuxrayWalkAway)
    // applymovement(LOCALID_FAN_1, Movement_Fan1WalkAway)
    // applymovement(LOCALID_FAN_2, Movement_Fan2WalkAway)
    waitmovement(LOCALID_PRIM)
    waitmovement(LOCALID_LUXRAY)
    // waitmovement(LOCALID_FAN_1)
    // waitmovement(LOCALID_FAN_2)
    removeobject(LOCALID_PRIM)
    removeobject(LOCALID_LUXRAY)
    // removeobject(LOCALID_FAN_1)
    // removeobject(LOCALID_FAN_2)

    msgbox(format(
        "{COLOR DARK_GRAY}Yeah! You should've just let her have it! She has better things to do than waste her time with scrubs like you! \p"
        "{COLOR DARK_GRAY}Yeah! She's Prim, the greatest idol to ever exist! She and Delcatty are gonna take the world by storm! \p"
        "{COLOR DARK_GRAY}You better believe she is! We stand with Her Majesty! \p"
        "{COLOR DARK_GRAY}E-Eh? W-Wait for us, Prim! C'mon Katelyn, let's leave these goobers!"
        ))
    closemessage
    applymovement(LOCALID_FAN_3, Movement_Fan3WalkAway)
    applymovement(LOCALID_FAN_4, Movement_Fan4WalkAway)

    waitmovement(LOCALID_FAN_3)
    waitmovement(LOCALID_FAN_4)
    removeobject(LOCALID_FAN_3)
    removeobject(LOCALID_FAN_4)

    setflag(FLAG_NORANNA_PRIM_CUTSCENE_FINISHED)
    setvar(VAR_CUTSCENE_FINISHED, 1)
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightLoser {
    lock
    msgbox(format("N-No way... She beat me without taking a scratch..."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightLombre {
    lock
    playmoncry(SPECIES_LOMBRE,CRY_MODE_NORMAL)
    msgbox(format("L-Lom...\p{COLOR RED}(Looks like Lombre took a thrashing.)"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec1 {
    msgbox(format("Prim is so pretty and confident. Her Pokémon fight so elegantly too.\pShe could be a Pokémon Coordinator!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec2 {
    lock
    faceplayer
    msgbox(format("That Prim sure is incredible... She only just became an idol a few months ago, and now she's already become the most popular celebrity in all of Mariyama.\pNow, she's on her way to completing the Mariyama Gym Challenge so she can enter in the upcoming Silverwing Festival Tournament!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_2_Interact_PrimFightSpec3 {
    lock
    faceplayer
    msgbox(format(
        "Ruka gets so many challenge requests that she can only take a limited number of them per day.\p"
        "These two were having a battle to decide who would get the last spot.\p"
        "Poor kid... He never stood a chance against Prim."
    ), MSGBOX_NPC)
}