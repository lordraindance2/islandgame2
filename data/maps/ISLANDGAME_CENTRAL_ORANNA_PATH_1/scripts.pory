const LOCALID_ENTRANCE_RANGER_1 = 1
const LOCALID_ENTRANCE_RANGER_2 = 2
const LOCALID_ENTRANCE_RANGER_3 = 3
const LOCALID_MIGHTYENA_1 = 4
const LOCALID_MIGHTYENA_2 = 5
const LOCALID_MIGHTYENA_3 = 6
const LOCALID_PKMN1 = 16
const LOCALID_PKMN2 = 17
const LOCALID_PKMN3 = 18

const POS_HIDDEN_X = 0
const POS_HIDDEN_Y = 0

const VAR_MIGHTYENA_HIDDEN = VAR_TEMP_0

mapscripts ISLANDGAME_CENTRAL_ORANNA_PATH_1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch (var(VAR_MINTY_MEADOWS_STATE)) {
            case 4:
                setvar(VAR_MIGHTYENA_HIDDEN, 1)
                setobjectxyperm(LOCALID_MIGHTYENA_1, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_MIGHTYENA_2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_MIGHTYENA_3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_1, 37, 27)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_ENTRANCE_RANGER_2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN1, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN2, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectxyperm(LOCALID_PKMN3, POS_HIDDEN_X, POS_HIDDEN_Y)
                setobjectmovementtype(LOCALID_ENTRANCE_RANGER_1, MOVEMENT_TYPE_FACE_DOWN)
        }
    }
}

movement Movement_PlayerPushBack {
    walk_down
}

movement Movement_MightyenaGrowl {
    jump_in_place_down
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Trigger_MightyenaTooDangerous {
    lock
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_NORMAL)
    applymovement(LOCALID_MIGHTYENA_1, Movement_MightyenaGrowl)
    waitmovement(0)
    waitmoncry
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(1, Common_Movement_FacePlayer)
    waitmovement(0)
    applymovement(1, Common_Movement_Delay32)
    msgbox(format("Hey! It's too dangerous to go any further! Please let us deal with this!"))
    closemessage
    applymovement(1, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_PlayerPushBack)
    waitmovement(0)
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC1 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC1,
        format("It's a lot of walking to get to Tidalcove. Let's battle instead!"),
        "Well, good luck with the rest of your\ntrip!"
    )
    msgbox(format("I heard that the trip is easier with a bike.\pBut the bike shop is in Tidalcove!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC2 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC2,
        format("There's no mountain that my team and I can't climb!"),
        "Climb higher through failure!"
    )
    msgbox(format("One day, I'm going to climb Mt. Aurora!\pIt'll be hard... but it's the only way to know if the rumors are true..."), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC3 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC3,
        format("Hey! There's only room for one bug catcher around here!"),
        "What's that? You don't care about\nbugs?"
    )
    msgbox(format("I'm looking for a cool bug around here. Have you found it yet?"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Battle_NPC4 {
    trainerbattle_single(TRAINER_CORANNA_1_NPC4,
        format("I don't think I'm ready for the Tidalcove Gym yet, so let's train!"),
        "Thanks for the battle!"
    )
    msgbox(format("It's okay to lose a couple of times. Life is a marathon, not a sprint!"), MSGBOX_NPC)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_EntranceRanger {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case 4: 
            faceplayer
            msgbox(
                format(
                    "Phew, we finally managed to get rid of that pack of wild Mightyena...\p"
                    "The leader of the pack was especially vicious and aggressive.\p"
                    "I've never seen anything like it, especially around these parts.\p"
                    "When it ran off, the other members of its pack followed suit.\p"
                    "I hope no one is in danger. Please be extra careful when traveling!"
                )
            )
        default: 
            faceplayer
            msgbox(format("Hey! It's too dangerous to go any further! Please let us deal with this!"))
            applymovement(1, Common_Movement_FaceOriginalDirection)
            waitmovement(0)
    }
    closemessage
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_EntranceSign {
    msgbox(format("CENTRAL ORANNA PATH\nMAPLEGROVE{RIGHT_ARROW}"), MSGBOX_SIGN)
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_ExtraRanger1 {
    lock
    msgbox(format("Linoone! Use slash!"))
    release
}

script ISLANDGAME_CENTRAL_ORANNA_PATH_1_Interact_ExtraRanger2 {
    lock
    msgbox(format("Watch out, Raticate!"))
    release
}